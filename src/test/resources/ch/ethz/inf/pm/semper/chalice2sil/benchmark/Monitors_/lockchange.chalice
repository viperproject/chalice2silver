/* There has been an incompleteness due to the previous handling of mu-fields 
 * and waitlevel-assertions. This incompleteness has been removed by also 
 * versioning mu-fields.
 *
 * I haven't removed the descriptions below to keep a description of what 
 * failed, and why, in case the current encoding still isn't complete or if
 * somebody wants to modify it.
 */

class Cell { var x: int}

class ProblemA {
	method acqThis()
		requires rd(mu) && waitlevel << mu
		ensures holds(this)
		/* Syxc currently (2011-02-25) needs the following two assertions in
		 * order to verify method test, which otherwise requires
		 * "lockchange this".
		 */
		/* The assumptions are needed so that in the test's post-state
		 *  - heap h contains this.mu |-> m'
		 *  - path conditions p contain m' = m, where m is this.mu's value in the 
		 *    pre-state
		 *
		 *  p also contains "waitlevel << mu" and together with "mu(this) = m'",
		 *  "m' = m"  and some axioms Syxc is able to infer that
		 *  forall r :: holds(r, pre-state) = holds(r, post-state).
		 */
		/* Solution candidate:
		 *  - give Z3 $Locks.mu terms an additional version argument
		 *  - have one mu-version counter for all mu-fields
		 *  - increment the counter each time a mu-field is written and add the
		 *    corresponding function update term to p
		 *  - create mu-terms from both heaps h, g
		 *
		 * The additional assumptions are not required by Chalice.
		 */

		// ensures rd(mu) && mu == old(mu) /* Was required by Syxc */
		lockchange this
	{ acquire this }
	
	method test() /* Previously failed  */
		requires rd(mu) && waitlevel << mu
	{
		call acqThis()
		release this
	}
}

class ProblemB {
  var c: Cell
  invariant rd(c) && c != null && rd(c.mu) && rd(this.mu) && this << c
  // invariant rd(c) // && c != null && rd(c.mu) && rd(this.mu) && this << c
	
	/* Same problem here: Updating c.mu destroys the link between pre-state
 	 * and post-state, therefore the verification fails.
	 * Looks like mu terms, e.g. mu(c) = tc, must also be versioned.
	 * Questions:
	 *   - When is the version incremented?
	 *   - When do we add 'mu(c, v) = m' terms to Ï€?
	 * Both times: when we produce a mu-chunk?
	 */
  method test() /* Previously failed  */
    requires rd(mu) && waitlevel << this
		/* Taken from the invariant. Using this clause makes the method verify. */
		// requires rd(c) && c != null && rd(c.mu) && this << c
    ensures rd(mu)
  {
    acquire this
		assert !holds(c)
		assert this.mu == waitlevel
    acquire c
    release c
    release this
  }
}

class SimpleTest {
	method test() /* @Error 340 */
		requires rd(mu) && waitlevel << this
	{ acquire this }
}